	PROGRAM I2SPEC4
C
C  THIS PROGRAM PREVIOUSLY CALLED I2SPECMODRA4
C  DIFFERENCE BETWEEN VERSION 4 AND VERSION 3 IS THAT
C  VERSION 4 ONLY CONSIDERS LINES WITHIN 0.2 CM-1 OF THE 
C  CURRENT FREQUENCY
C  THIS PROGRAM WAS WRITTEN BY J. FORKEY WHILE WORKING WITH W. LEMPERT AND
C  R. MILES AT PRINCETON UNIVERSITY'S MECHANICAL AND AEROSPACE ENGINEERING
C  DEPARTMENT
C  MOST RECENT REVISIONS MADE IN APRIL 1996

C  THIS PROGRAM MODELS THE TRANSMISSION OF AN IODINE CELL, GIVEN ITS LENGTH,
C  TEMPERATURE, AND PRESSURE.
C  LINE LOCATIONS, FRANCK-CONDON FACTORS, AND TRANSITION QUANTUM NUMBERS, 
C  ARE TAKEN FROM THE FILE FILIN, WHICH IS GENERATED BY THE PROGRAM
C  I2LINES2 (ASSIGNLINERA2) WHICH USES THE SPECTROSCOPIC CONSTANTS AND FRANCK-
C  CONDON FACTORS TABULATED IN THE PUBLICATION "IDENTIFICATION DES
C  TRANSITIONS DU SYSTEME (B-X) DE LA MOLECULE D'IODE ET FACTEURS DE FRANCK-
C  CONDON 14000-15600 CM-1" BY S. GERSTENKORN, AND P. LUC (PUBLISHED BY
C  LABORATOIRE AIME - COTTON C.N.R.S. II 91405 ORSAY, FRANCE, 1986).
C  NUMBER DENSITIES ARE CALCULATED USING THE IDEAL GAS LAW, AND A PARTITION 
C  FUNCTION USING ENERGY LEVELS CALCULATED USING CONSTANTS IN GERSTENKORN
C  AND LUC.
C  HYPERFINE STRUCTURE (DUE TO 1ST ORDER NUCLEAR ELECTRIC QUADRAPOLE
C  INTERACTION AND MAGNETIC NUCLEAR SPIN - MOLECULAR ROTATION INTERACTION)
C  IS MODELED BASED ON KROLL & INNES,J. MOL.SPECT., V36,P295-309,
C  ON LEVENSON & SCHAWLOW, PHYS.REV.A VOL.6, (1972) 10-20, AND WITH THE
C  HYPERFINE STRUCTURE CONSTANTS CALCULATED IN I2LINES2 (ASSIGNLINESRA2) USING 
C  THE FORMULAS IN GLASER, OPTICS COMM., 54, 335-342 (1985) THE EXPRESSIONS 
C  FOR ABSORPTION CONSTANT ARE TAKEN FROM STEINFELD'S BOOK, "MOLECULAR
C  SPECTROSCOPY" AND FROM TELLINGHUISEN, J. QUANT. SPECT. RADIAT. TRANSFER,
C  V19, (1978) 149-161. 
C  AVERAGE ELECTRONIC TRANSITION STRENGTHS ARE TAKEN FROM TELLINGHUISEN, 
C  J. CHEM. PHYS. V76 (1982) 4736-4744, OR FOR FREQUENCIES CLOSE TO DOUBLED
C  ND:YAG, FROM EXPERIMENTS PERFORMED AT PRINCETON UNIVERSITY.
C  THIS PROGRAM MODELS EACH HYPERFINE LINE AS A DOPPLER BROADENED LINE,
C  AND IT ASSUMES THAT THE PEAK OPTICAL DEPTHS OF THE HYPERFINE LINES
C  ASSOCIATED WITH A GIVEN VIBRATIONAL, ROTATIONAL TRANSITION ARE THE SAME
C  (STRICTLY TRUE ONLY FOR "LARGE J" VALUES).
C
	DOUBLE PRECISION CENTWN(1:50000),CENTHYPWN(1:50000,1:6,1:6),
     &	     TRANS(500000)
	REAL TEMP,PRES,RLEN,AETS,CENTABS(1:50000),FCF(1:50000),
     &       RNUM(1:50000),WIDTHHYPWN(1:50000,1:6,1:6),WNSTART,WNSTOP,
     &	     WNSTEP,DELTAEQQ(1:50000),DELTAC(1:50000)
	INTEGER IVL(1:50000),IVH(1:50000),IJ(1:50000),NLINES
	CHARACTER*1 PORR(1:50000)
	CHARACTER*20 FILIN
	COMMON/LINEINFO/CENTWN,FCF,IVL,IVH,IJ,PORR,DELTAEQQ,DELTAC
C
C  VARIABLES:
C    CENTWN(I)=	WAVENUMBER OF CENTER OF VIB., ROT. TRANSITION I
C    CENTHYPWN(I,M1,M2)=WAVENUMBER OF CENTER OF HYPERFINE TRANSITION 
C	ASSOCIATED WITH VIB., ROT. TRANSITION I, AND WITH NUCLEAR M VALUES 
C	EQUAL TO M1 AND M2. 
C    WIDTHHYPWN(I,K)=DOPPLER WIDTH, IN WAVENUMBERS, OF HYPERFINE TRANSITION 
C	ASSOCIATED WITH VIB., ROT. TRANSITION I, AND WITH NUCLEAR M VALUES 
C	EQUAL TO M1 AND M2
C    FCF(I)=FRANK-CONDON FACTOR OF TRANSITION I
C    PORR(I)=INDICATES WHETHER VIB., ROT. TRANSITION I IS A P OR AN R LINE
C    RNUM(I)=NUMBER OF MOLECULES PER CM3, IN CORRECT LOWER VIB., ROT. STATE 
C	TO BE EXCITED BY TRANSITION I, DIVIDED BY THE NUMBER OF HYPERFINE 
C	TRANSITIONS IN THIS VIB., ROT. LEVEL (I.E. NUMBER OF MOLECULES IN 
C	ONE HYPERFINE LEVEL OF THE VIB., ROT. STATE CORRESPONDING TO THIS 
C	TRANSITION
C    IVL(I)=VIBRATIONAL QUANTUM NUMBER OF LOWER ELECTRONIC STATE (X) FOR
C	VIB., ROT. TRANSITION I
C    IVH(I)=VIBRATIONAL QUANTUM NUMBER OF UPPER ELECTRONIC STATE (B) FOR
C	VIB., ROT. TRANSITION I
C    IJ(I)=ROTATIONAL QUANTUM NUMBER OF LOWER ELECTRONIC STATE (X) FOR 
C	VIB., ROT. TRANSITION I (ROT. QUANT. NUM. OF UPPER STATE IS DETERMINED
C	BY PORR(I))
C    CENTABS(I)=ABSORPTION COEFFICIENT (INCLUDING LENGTH FACTOR) AT CENTER
C	OF EACH HYPERFINE TRANSITION WITHIN VIB., ROT., TRANSITION I ( THIS 
C	IS ASSUMED TO BE THE SAME FOR EACH HYPERFINE TRANSITION ASSOCIATED 
C	WITH A GIVEN VIB., ROT. TRANSITION)
C    TEMP=TEMPERATURE OF CELL (KELVIN)
C    PRES=PRESSURE OF CELL (TORR)
C    RLEN=LENGTH OF CELL (CM)
C    AETS=AVERAGE ELECTRONIC TRANSITION STRENGTH
C    WNSTEP=WAVENUMBER RESOLUTION
C    WNSTART=STARTING WAVENUMBER
C    WNSTOP=ENDING WAVENUMBER
C    TRANS(K)=TRANSMISSION AT WAVENUMBER=WNSTOP+K*WNSTEP
C    NLINES=NUMBER OF LINES USED IN MODEL 
C    DELTAEQQ(I)=NUCLEAR ELECTRIC QUADRUPOLE HYPERFINE STRUCTURE CONSTANT 
C	ASSOCIATED WITH THE VIB., ROT. TRANSITON I
C    DELTAC(I)=NUCLEAR SPIN - MOLECULAR ROTATION HYPERFINE STRUCTURE
C	CONSTANT
C    FILIN=NAME OF DATA FILE GENERATED BY I2LINES2 (ASSIGNLINERA2) WITH 
C       LINE DATA IN IT
C
	CALL GETPARS(TEMP,PRES,RLEN,WNSTART,WNSTOP,WNSTEP)
	CALL ASSIGNLINES(FILIN,NLINES)
	CALL CALCNUMS(TEMP,PRES,IVL,IJ,RNUM,NLINES)
	CALL CALCCENTS(CENTWN,CENTHYPWN,NLINES,DELTAEQQ,DELTAC,IJ)
	CALL CALCWIDTHS(CENTHYPWN,TEMP,WIDTHHYPWN,RNUM,NLINES,IJ)
	CALL GETAETS(((WNSTART+WNSTOP)/2.),AETS)
	CALL CALCMAXABS(FCF,AETS,TEMP,RNUM,RLEN,CENTABS,NLINES)
	CALL CALCTRANS(WNSTART,WNSTOP,WNSTEP,CENTABS,CENTHYPWN,
     &                 WIDTHHYPWN,IJ,TRANS,NLINES,CENTWN)
	CALL DATAOUT(TRANS,WNSTART,WNSTOP,WNSTEP,TEMP,PRES,AETS,RLEN,
     &              FILIN)
C
	STOP
	END

****************************************************************************

	SUBROUTINE GETPARS(TEMP,PRES,RLEN,WNSTART,WNSTOP,WNSTEP)
C  THIS SUBROUTINE GETS INPUT FROM THE KEYBOARD
C
	REAL TEMP,PRES,RLEN,WNSTART,WNSTOP,WNSTEP
C
	WRITE(6,10)
10	FORMAT('ENTER TEMPERATURE OF CELL [K]')
	READ(5,*)TEMP
	WRITE(6,20)
20	FORMAT('ENTER PRESSURE OF CELL [TORR]')
	READ(5,*)PRES
	WRITE(6,30)
30	FORMAT('ENTER LENGTH OF CELL [CM]')
	READ(5,*)RLEN
	WRITE(6,50)
50	FORMAT('ENTER STARTING WAVENUMBER [CM-1]')
	READ(5,*)WNSTART
	WRITE(6,60)
60	FORMAT('ENTER ENDING WAVENUMBER [CM-1]')
	READ(5,*)WNSTOP
	WRITE(6,70)
70	FORMAT('ENTER RESOLUTION [CM-1]')
	READ(5,*)WNSTEP
C
	RETURN
	END

****************************************************************************

	SUBROUTINE ASSIGNLINES(FILIN,IEND)
C  THIS SUBROUTINE LOADS DATA INTO VARIABLE ARRAYS FROM THE FILE FILIN WHICH
C  WAS GENERATED BY THE PROGRAM I2LINES2 (ASSIGNLINERA2)
C
	DOUBLE PRECISION CENTWN(1:50000)
	REAL FCF(1:50000),DELTAEQQ(1:50000),DELTAC(1:50000)
	INTEGER IVL(1:50000),IVH(1:50000),IJ(1:50000),IEND
	CHARACTER*1 PORR(1:50000)
	CHARACTER*20 FILIN
	COMMON/LINEINFO/CENTWN,FCF,IVL,IVH,IJ,PORR,DELTAEQQ,DELTAC
C
	WRITE(6,80)
80	FORMAT('ENTER NAME OF FILE WITH LINE DATA IN IT')
	READ(5,90)FILIN
90	FORMAT(A20)
C
	OPEN(UNIT=30,FILE=FILIN)
	READ(30,*)IEND
	DO 10 I=1,IEND
	  READ(30,15)CENTWN(I),IVH(I),IVL(I),IJ(I),PORR(I),FCF(I),
     &		     DELTAEQQ(I),DELTAC(I)
15	  FORMAT(F10.4,1X,I2,1X,I2,1X,I3,1X,A1,1X,E11.4,1X,E13.6,
     &		 1X,E13.6)
10	CONTINUE
C
	RETURN
	END

******************************************************************************

	SUBROUTINE CALCNUMS(TEMP,PRES,IVL,IJ,RNUM,NLINES)
C  THIS SUBROUTINE CALCULATES THE TOTAL NUMBER DENSITY (PER CM3) OF
C  MOLECULES USING THE IDEAL GAS LAW, AND THEN CALCULATES THE DENSITY (PER
C  CM3) OF MOLECULES FOR EACH VIBRATION - ROTATION STATE IN THE LOWER
C  ELECTRONIC LEVEL (X), THAT IS INVOLVED IN THE TRANSITIONS CONSIDERED IN THE
C  PROGRAM. FINALLY, THIS NUMBER IS DIVIDED BY THE NUMBER OF NUCLEAR SPIN
C  STATES (SEE KROLL & INNES) THAT ARE POSSIBLE FOR THE GIVEN IJ VALUE (ODD
C  IJ - 21, EVEN IJ - 15). THIS ASSUMES THAT THE ENERGY LEVELS ASSOCIATED WITH THE
C  DIFFERENT NUCLEAR SPIN STATES ALL HAVE APPROXIMATELY THE SAME ENERGY,
C  SO THAT THE "RELATIVE BOLTZMANN FACTORS" ARE ABOUT 1
C  (I.E. EXP((ENUCLEAR1-ENUCLEAR2)/KT)=1). SPECTROSCOPIC CONSTANTS 
C  ARE TAKEN FROM GERSTENKORN AND LUC'S BOOK.
C
	  REAL RNUM(1:50000),RNTOT,ENERGYWN,RTDEGEN,SPDEGEN,PTFN
	  INTEGER IVL(1:50000),IJ(1:50000)
	  DOUBLE PRECISION EL(0:19),BL(0:19),DL(0:19),HL(0:19)
	  COMMON/CONSTS/EL,BL,DL,HL
C
      DATA EL /0.0000, 213.3023, 425.3742, 636.2103, 845.8034,
     &           1054.1456, 1261.2284, 1467.0428, 1671.5796, 1874.8295, 
     &           2076.7829, 2277.4300, 2476.7606, 2674.7643, 2871.4302, 
     &           3066.7470, 3260.7032, 3453.2871, 3644.4874, 3834.2932/
      DATA BL /0.3731114098671146, 0.3719670067802829,
     &       0.3708161358474350,
     &       0.3696585853818199, 0.3684941526048041, 0.3673226380482058,
     &       0.3661438399566286, 0.3649575486897959, 0.3637635411248850,
     &	     0.3625615750588612, 0.3613513836108117, 0.3601326696242798,
     &	     0.3589051000695991, 0.3576683004462275, 0.3564218491850814,
     &	     0.3551652720508696, 0.3538980365444274, 0.3526195463050510,
     &	 0.3513291355128313, 0.3500260632909881/
      DATA DL /0.4547547628207550, 0.4572208485138791, 
     &   0.4597534338572310,
     &	 0.4623730064958022, 0.4650910671516871, 0.4679135797367788,
     &	 0.4708437425553093, 0.4738840805962359, 0.4770378589154716,
     &	 0.4803098171079618, 0.4837062248696054, 0.4872342586490214,
     &	 0.4909006993891605, 0.4947099513587618, 0.4986613820736552,
     &	 0.5027459833079082, 0.5069423531948184, 0.5112119994177510,
     &	 0.5154939634908213, 0.519698766129423/
      DATA HL /0.5127518681623231, 0.5340322291154361, 
     &   0.5556429468721693,
     &	 0.5779567669609448, 0.6012238577098949, 0.6256071108097138,
     &	 0.6512174418765087, 0.6781490910146517, 0.7065149233796305,
     &	 0.7364817297409003, 0.7683055270447346, 0.8023668589770773,
     &	 0.8392060965263935, 0.8795587385465209, 0.9243907123195214,
     &	 0.9749336741185324, 1.032720309770618, 1.099619635219621,
     &	 1.177872297089012, 1.270125873244745/
      
      DO 5 I = 0,19
      BL(I)=BL(I)*0.1
      DL(I)=DL(I)*1.D-8
      HL(I)=HL(I)*(-1.D-15)
5     CONTINUE

      RNTOT=9.654E18*PRES/TEMP
      PTFN=PARTFCN(TEMP)
      DO 10 I=1,NLINES
      GAMMA=(FLOAT(IJ(I)))*(FLOAT(IJ(I)+1))
      ENERGYWN=EL(IVL(I))+(BL(IVL(I))*GAMMA)-(DL(IVL(I))*(GAMMA**2.))
     &		   +(HL(IVL(I))*(GAMMA**3.))
	  RTDEGEN=2.*FLOAT(IJ(I))+1.
	  IF (MOD(IJ(I),2).EQ.0) THEN
	    SPDEGEN=15.
	  ELSE IF (MOD(IJ(I),2).EQ.1) THEN
	    SPDEGEN=21.
	  ENDIF
	  RNUM(I)=RNTOT*SPDEGEN*RTDEGEN
     &            *EXP(-ENERGYWN/(0.6950*TEMP))/PTFN
C  SINCE SPIN STATES OF NUCLEI WERE ALL TAKEN TO BE THE SAME, THIS RNUM
C  IS THE NUMBER DENSITY OF MOLECULES IN A GIVEN VIBRATIONAL ROTATIONAL
C  STATE, WITH ANY NUCLEAR SPIN QUANTUM NUMBERS.  THE PROGRAM, HOWEVER
C  CALLS FOR THE NUMBER DENSITY OF MOLECULES WITH SPECIFIC NUCLEAR SPIN
C  QUANTUM NUMBERS WITHIN A GIVEN VIBRATIONAL ROTATIONAL LEVEL.
C  THEREFORE, DIVIDE BY THE NUCLEAR SPIN DEGENERACY.
	  RNUM(I)=RNUM(I)/SPDEGEN
10	  CONTINUE
C
	  RETURN
	  END

***************************************************************************

	FUNCTION PARTFCN(TEMP)
C  THIS FUNCTION CALCULATES THE PARTITION FUNCTION FOR IODINE, BY SUMMING
C  UP ALL OF THE TERMS FOR THE X ELECTRONIC STATE, WITH VIBRATIONAL  
C  QUANTUM NUMBER BETWEEN 0 AND 19, AND ROTATIONAL QUANTUM NUMBER
C  BETWEEN 0 AND 200, USING THE CONSTANTS IN GERSTENKORN AND LUC'S BOOK.
C  NOTE THAT THE DEGENERACY INCLUDES THE DEGENERACY OF A VIBRATIONAL
C  ROTATIONAL STATE DUE TO POSSIBLE NUCLEAR SPIN STATES

	INTEGER KV,KJ
	REAL TEMP,ENERGYWN,SUMROT,SUM,SPDEG,PARTFCN
	DOUBLE PRECISION EL(0:19),BL(0:19),DL(0:19),HL(0:19)
	COMMON/CONSTS/EL,BL,DL,HL
C
C  ADD UP TERMS IN PARTITION FUNCTION
	  SUM=0.
      DO 60 KV=0,19
      SUMROT=0.
      DO 50 KJ=0,200
	    ENERGYWN=EL(KV)+BL(KV)*FLOAT(KJ)*(FLOAT(KJ)+1.)
     &		    -DL(KV)*(FLOAT(KJ)**2)*((FLOAT(KJ)+1.)**2)
     &		    +HL(KV)*(FLOAT(KJ)**3)*((FLOAT(KJ)+1.)**3)
        IF (MOD(KJ,2).EQ.0) THEN
         SPDEG=15.
        ELSE IF (MOD(KJ,2).EQ.1) THEN
         SPDEG=21.
        ENDIF
        SUMROT=SUMROT+SPDEG*(2.*FLOAT(KJ)+1.)
     &             *EXP(-1.*ENERGYWN/(0.695*TEMP))
        ROTLAST=SPDEG*(2.*FLOAT(KJ)+1.)
     &		    *EXP(-1.*ENERGYWN/(0.695*TEMP))
      IF ((KJ.EQ.200).AND.((ROTLAST/SUMROT).GT.(.05))) THEN
      WRITE(*,20)KV
20    FORMAT('THE LAST ROTATIONAL TERM FOR V = ',I2,' IN THE')
      WRITE(*,30)
30    FORMAT('PARTITION FUNCTION, IS GREATER THAN 5% OF THE SUM')
      WRITE(*,40)
40    FORMAT('OF ALL OF THE OTHER ROTATIONAL TERMS IN THIS')
      WRITE(*,45)
45    FORMAT('VIBRATIONAL LEVEL - MORE ROTATIONAL LEVELS MAY BE')
      WRITE(*,48)
48    FORMAT('NEEDED FOR THIS TEMPERATURE')
      ENDIF
50    CONTINUE
      SUM=SUM+SUMROT
60    CONTINUE
      
      IF ((SUMROT/SUM).GT.(.05)) THEN
        WRITE(*,70)
70      FORMAT('THE CONTRIBUTION TO THE PARTITION')
        WRITE(*,80)
80      FORMAT('WITH V=19 IS LARGER THAN 5% OF THE SUM')
        WRITE(*,90)
90      FORMAT('THE LOWER VIBRATIONAL LEVELS - MORE VIBRATIONAL LEVELS')
        WRITE(*,100)
100     FORMAT('MAY BE NEEDED FOR THIS TEMPERATURE')
      ENDIF
      
      PARTFCN=SUM
C
      RETURN
      END

*****************************************************************************

	SUBROUTINE CALCCENTS(CENTWN,CENTHYPWN,NLINES,DELTAEQQ,DELTAC,IJ)
C  THIS SUBROUTINE CALCULATES THE POSITIONS OF THE HYPERFINE LINES (IN 
C  WAVENUMBERS) GIVEN THE POSITIONS OF THE VIBRATION-ROTATION TRANSITIONS.
C  THE NUCLEAR ELECTRIC QUADRUPOLE HYPERFINE INTERACTION AND THE MAGNETIC
C  NUCLEAR SPIN - MOLECULAR ROTATION HYPERFINE INTERACTION ARE INCLUDED.
C  THE EQUATIONS USED ARE TAKEN FROM LEVENSON AND SCHAWLOW WHICH ARE,
C  STRICTLY SPEAKING VALID ONLY FOR "LARGE J" 
C
	DOUBLE PRECISION CENTWN(1:50000),CENTHYPWN(1:50000,1:6,1:6)
	REAL DELTAEQQ(1:50000),DELTAC(1:50000),RM1,RM2,RJ
	INTEGER NLINES,IJ(1:50000),M1,M2,M2MAX
C
	DO 30 I=1,NLINES
	  DO 20 M1=1,6
	    IF (MOD(IJ(I),2).EQ.0) THEN
	      M2MAX=M1-1
	    ELSE
	      M2MAX=M1
	    ENDIF
	    DO 10 M2=1,M2MAX
	      RM1=FLOAT(M1)-3.5
	      RM2=FLOAT(M2)-3.5
	      RJ=FLOAT(IJ(I))
	      IF (IJ(I).NE.0) THEN
	        CENTHYPWN(I,M1,M2)=CENTWN(I)+
     &	  	  DELTAC(I)*(RJ*(RM1+RM2)
     &		    +0.5*(RM1*(RM1+1.)+RM2*(RM2+1.))-8.75)
     &	         -((DELTAEQQ(I)/80.)*(3.*(RM1**2.+RM2**2.)
     &	  +(3./RJ)*(RM1*(RM1*(RM1+1.)-8.25)+RM2*(RM2*(RM2+1.)-8.25))
     &		  -17.5))
	      ELSE
	        CENTHYPWN(I,M1,M2)=CENTWN(I)
	      ENDIF
10	    CONTINUE
20	  CONTINUE
30	CONTINUE
C
	RETURN
	END

****************************************************************************

	SUBROUTINE CALCWIDTHS(CENTHYPWN,TEMP,WIDTHHYPWN,RNUM,NLINES,IJ)
C  THIS SUBROUTINE CALCULATES THE HALF WIDTH AT 1/e MAX FOR EACH OF THE
C  HYPERFINE LINES
C
	DOUBLE PRECISION CENTHYPWN(1:50000,1:6,1:6)
	REAL RNUM(1:50000), WIDTHHYPWN(1:50000,1:6,1:6)
	INTEGER IJ(1:50000),M1,M2,M2MAX
C
	DO 30 I=1,NLINES
	  DO 20 M1=1,6
	    IF (MOD(IJ(I),2).EQ.0) THEN
	      M2MAX=M1-1
	    ELSE
	      M2MAX=M1
	    ENDIF
	    DO 10 M2=1,M2MAX
	  WIDTHHYPWN(I,M1,M2)=(2.700E-8)*CENTHYPWN(I,M1,M2)*(SQRT(TEMP))
10	    CONTINUE
20	  CONTINUE
30	CONTINUE
C
	RETURN
	END

****************************************************************************

	SUBROUTINE GETAETS(WNUM,AETS)
C  THIS SUBROUTINE DETERMINES THE AVERAGE ELECTRONIC TRANSITION STRENGTH
C  OF THE B TO X TRANSITION TO BE USED IN THE CALCULATION OF CELL
C  TRANSMISSION.  THIS SUBROUTINE USES EITHER THE VALUE FOR AETS TABULATED IN
C  TELLINGHUISEN (1982) (AFTER USING LINEAR INTERPOLATION), OR IT USES THE VALUE
C  OF 0.99E-36 WHICH WAS DETERMINED AT PRINCETON FOR THE FREQUENCY DOUBLED
C  ND:YAG WAVELENGTH REGION (18787CM-1 TO 18789CM-1).  THIS VALUE IS BASED ON 57
C  MEASURED VALUES AND IS PROBABLY MORE ACCURATE THAN THE VALUE OF
C  1.06E-36 GIVEN BY TELLINGHUISEN (1982) FOR THIS WAVELENGTH REGION, AND FOR
C  THE FCFS USED IN THIS PROGRAM.
C
	DIMENSION STRENGTH(45:63)
	REAL WNUM,AETS,WAVE
	INTEGER I,J
C
	DATA STRENGTH /0.56, 0.63, 0.67, 0.72, 0.78, 0.83, 0.92, 0.94,
     &                 1.06, 1.05, 1.02, 1.06, 1.01, 1.01, 1.01, 1.23,
     &                 1.07, 1.27, 0.95/
C
5	WRITE(6,10)
10	FORMAT('DO YOU WANT TO USE PUBLISHED VALUES FOR THE AVERAGE')
	WRITE(6,11)
11     FORMAT('ELECTRONIC TRANSITION STRENGTH, OR THE VALUE DETERMINED')
	WRITE(6,12)
12     FORMAT('EXPERIMENTALLY AT PRINCETON FOR DBLED ND:YAG?')
	WRITE(6,13)
13    FORMAT('TYPE 1 TO USE PUBLISHED VALUES (FOR ALL WAVENUMBERS OTHER
     &THAN THOSE')
	WRITE(6,14)
14	FORMAT('           CLOSE TO 18788CM-1)')
	WRITE(6,15)
15      FORMAT('TYPE 2 TO USE PRINCETON VALUE (FOR FREQUENCY DOUBLED
     &  ND:YAG I.E. CLOSE ')
	WRITE(6,16)
16	FORMAT('           TO 18788CM-1)')
	READ(5,20)J
20	FORMAT(I1)
C
	  IF (J.EQ.1) THEN
	  WAVE=1.0E7/WNUM
	  I=NINT(WAVE/10)
        AETS=STRENGTH(I)+(WAVE/10.-FLOAT(I))*(STRENGTH(I+1)-STRENGTH(I))
	  AETS=AETS*1.E-36
	  ELSEIF (J.EQ.2) THEN
	  AETS=0.99E-36
C	  AETS=0.97E-36
C	  AETS=0.94E-36
	  ELSE
	  GOTO 5
	  ENDIF
C
	RETURN
	END

*****************************************************************************

	SUBROUTINE CALCMAXABS(FCF,AETS,TEMP,RNUM,RLEN,CENTABS,NLINES)
C  THIS SUBROUTINE CALCULATES THE PEAK OPTICAL DEPTH AT LINECENTER FOR
C  EACH VIB-ROT LINE (THIS IS ASSUMED TO BE THE SAME FOR ALL HYPERFINE 
C  LINES ASSOCIATED WITH A GIVEN VIB-ROT LINE).
C
	REAL FCF(1:50000),RNUM(1:50000),AETS,TEMP,RLEN,CENTABS(1:50000)
C
	DO 20 I=1,NLINES
           CENTABS(I)=RLEN*(RNUM(I)/(SQRT(TEMP)))*(4.348E24)*AETS*FCF(I)
20	CONTINUE
C
	RETURN
	END

***************************************************************************

	SUBROUTINE CALCTRANS(WNSTART,WNSTOP,WNSTEP,CENTABS,CENTHYPWN,
     &                       WIDTHHYPWN,IJ,TRANS,NLINES,CENTWN)
C  THIS SUBROUTINE CALCULATES THE TRANSMISSION OF THE IODINE CELL (NOT
C  INCLUDING WINDOW ABSORPTION, REFLECTION ETC.), FOR WNSTART WAVENUMBERS
C  TO WNSTOP WAVENUMBERS, AT INTERVALS OF WNSTEP WAVENUMBERS.
C
	DOUBLE PRECISION CENTHYPWN(1:50000,1:6,1:6),WAVENUM,
     &       TRANS(500000),CENTWN(1:50000)
	REAL WNSTART,WNSTOP,WNSTEP,CENTABS(1:50000),
     &       WIDTHHYPWN(1:50000,1:6,1:6)
	INTEGER IJ(1:50000),M1,M2,M2MAX,ILO,IHI
C
	DO 10 M=1,500000
	  TRANS(M)=1.
10	CONTINUE
	ILO=1
	IHI=2
	NPTS=NINT((WNSTOP-WNSTART)/WNSTEP)
	DO 130 N=1,NPTS
	  WAVENUM=DBLE(WNSTART)+DBLE(FLOAT(N))*DBLE(WNSTEP)
	  IF (DMOD(WAVENUM,DBLE(1.0)).LT.WNSTEP) THEN
	    WRITE(6,20)WAVENUM
20	    FORMAT ('WORKING ON 'F11.4' CM-1')
	  ENDIF
50	  IF ((WAVENUM-CENTWN(ILO)).GE.(0.2)) THEN
	    ILO=ILO+1
	    GOTO 50
	  ENDIF
60	  IF (((CENTWN(IHI+1)-WAVENUM).LE.(0.2)).AND.
     &        (IHI.NE.NLINES))  THEN
	    IHI=IHI+1
	    GOTO 60
	  ENDIF
	  DO 120 I=ILO,IHI
	    DO 110 M1=1,6
	      IF (MOD(IJ(I),2).EQ.0) THEN
	        M2MAX=M1-1
	      ELSE
	        M2MAX=M1
	      ENDIF
	      DO 100 M2=1,M2MAX
	        TRANS(N)=TRANS(N)*EXP(-1.*CENTABS(I)*
     &                   EXP(-1.*(((WAVENUM-CENTHYPWN(I,M1,M2))/
     &			            WIDTHHYPWN(I,M1,M2))**2.)))
100	      CONTINUE
110	    CONTINUE
120	  CONTINUE
130	CONTINUE
C
	RETURN
	END

****************************************************************************

	SUBROUTINE DATAOUT(TRANS,WNSTART,WNSTOP,WNSTEP,TEMP,PRES,AETS,
     &			   RLEN,FILIN)
C  THIS SUBROUTINE SENDS THE DATA TO AN OUTPUT FILE
C
	DOUBLE PRECISION WAVENUM,TRANS(500000)
	REAL WNSTART,WNSTOP,WNSTEP,TEMP,PRES,AETS,RLEN
	CHARACTER*20 FILNAM,FILIN
	CHARACTER*30 HEADER
C	
	HEADER = '# OUTPUT FROM I2SPEC4 #'
	WRITE(6,10)
10	FORMAT('ENTER OUTPUT DATA FILE NAME')
	READ(5,15)FILNAM
15	FORMAT(A20)
	OPEN(UNIT=40,FILE=FILNAM)
	WRITE(40,20) HEADER
20	FORMAT(A30)
	WRITE(40,21)FILIN
21	FORMAT('# LINE POSITIONS TAKEN FROM FILE ',A20,' #')
	WRITE(40,22)TEMP
22	FORMAT('# TEMP = 'F10.5' K #')
	WRITE(40,24)PRES
24	FORMAT('# PRES = 'F10.5' TORR #')
	WRITE(40,25)RLEN
25	FORMAT('# CELL LENGTH = 'F10.5' CM #')
	WRITE(40,26)AETS
26	FORMAT('# AETS = 'E15.5' [ESU^2 CM^2] #')
	WRITE(40,27)WNSTART,WNSTOP
27	FORMAT('# START,END WAVENUMBERS ARE 'F15.5,1X,F15.5,' #')
	WRITE(40,28)WNSTEP
28	FORMAT('# RESOLUTION IS 'F10.5' #')
	NPTS=NINT((WNSTOP-WNSTART)/WNSTEP)
	DO 30 N=1,NPTS
	  WAVENUM=DBLE(WNSTART)+DBLE(FLOAT(N))*DBLE(WNSTEP)
	  WRITE(40,29)WAVENUM,TRANS(N)
29	  FORMAT(F15.5,1X,E14.5)
30	CONTINUE
	CLOSE (40)
C
	RETURN
	END


